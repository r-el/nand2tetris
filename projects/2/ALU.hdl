// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zx ? 0 : x
    Mux16(a=x, b[0..15]=false, sel=zx, out=x0); 

    // nx ? !x : x
    Not16(in=x0, out=notx);
    Mux16(a=x0, b=notx, sel=nx, out=xval);

    // zy ? 0 : y
    Mux16(a=y, b[0..15]=false, sel=zy, out=y0);
    
    // ny ? !y : y
    Not16(in=y0, out=noty);
    Mux16(a=y0, b=noty, sel=ny, out=yval);

    // f ? x + y : x & y
    Add16(a=xval, b=yval, out=sum);
    And16(a=xval, b=yval, out=and);
    Mux16(a=and, b=sum, sel=f, out=out1);

    // no ? !out : out
    Not16(in=out1, out=notout);
    Mux16(a=out1, b=notout, sel=no, out=out, out[0..7]=o0to7, out[8..15]=o8to15, out[15]=ng); // ng = out[15]

    // zr = not (or out[0..7] | out[8..15])
    Or8Way(in=o0to7, out=or0to7);
    Or8Way(in=o8to15, out=or8to15);
    Or(a=or0to7, b=or8to15, out=or);
    Not(in=or, out=zr);
}